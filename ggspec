#!/usr/bin/guile \
-e main -s
ggspec - run a set of test suites in your project's 'spec' subdirectory.

Copyright (c) 2014 Yawar Amin
GitHub, Reddit, Twitter: yawaramin

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
!#
(use-modules
  (srfi srfi-1)
  (ggspec lib)
  (ice-9 ftw)
  (ice-9 getopt-long)
  (ice-9 match))

(define version-num "1.2")
(define release-name "Gemini")

(define (suite-add-option opt s)
  "Add an option to the read, unevaluated form of a suite.

  Arguments
    opt: '(option k v)

      k: symbol: name of the option.
      v: any; value of the option.

    s: form: a read, unevaluated 'suite' form:

      (suite desc tsts opts sups tdowns)

  Returns
    A read, unevaluated suite with the option added."
  (define (opts-add-opt os o)
    (cons 'options (cons o (cdr os))))

  #!
  Below, variable names have the following meanings:

  d: suite description
  ts: a list of tests
  os: a list of options
  ss: a list of setups
  tds: a list of teardowns
  !#
  (match s
    (('suite d ts) (list 'suite d ts (list 'options opt)))
    (('suite d ts os) (list 'suite d ts (opts-add-opt os opt)))
    (('suite d ts os ss) (list 'suite d ts (opts-add-opt os opt) ss))
    (('suite d ts os ss tds)
      (list 'suite d ts (opts-add-opt os opt) ss tds))))

(define (run-file fname opts)
  "Run all test suites found in the given file, and aggregate and return
  the number of passed and failed tests.

  Arguments
    fname: string: the name of the file to look in for test suites.

  Returns
    (list num-passes num-fails): same as in the suite function."
  (call-with-input-file
    fname
    (lambda (f)
      (let loop
        ((form (read f))
        (num-passes 0)
        (num-fails 0)
        (num-skips 0))

        (cond
          ((eof-object? form)
            (list num-passes num-fails num-skips))
          ((equal? (car form) 'suite)
            ;; Add options to the current suite and run it.
            (let
              ((results
                (eval
                  (fold suite-add-option form opts)
                  (current-module))))

              (loop
                (read f)
                (+ num-passes (car results))
                (+ num-fails (cadr results))
                (+ num-skips (caddr results)))))
          (#t
            ;; This is some form other than a suite definition.
            (begin
              (eval form (current-module))
              ;; Go on to the next form, with results unchanged.
              (loop (read f) num-passes num-fails num-skips))))))))

(define (run-tree dname opts)
  "Run all test suites found in files in the given directory and all its
  subdirectories, and aggregate and return the number of passed and
  failed tests.

  Arguments
    dname: string: the name of the directory to look in for test suites.

  Returns
    (list num-passes num-fails): same as in the suite function."
  (define passed 0)
  (define failed 0)
  (define skipped 0)

  (nftw
    dname
    (lambda (filename statinfo flag base level)
      (define filename-length (string-length filename))

      (if (equal? (substring filename (- filename-length 4)) ".scm")
        (let ((file-results (run-file filename opts)))
          (set! passed (+ passed (car file-results)))
          (set! failed
            (+ failed (cadr file-results)))
          (set! skipped
            (+ skipped (caddr file-results)))))

      #t))

  (list passed failed skipped))

(define (show-help)
  (println "ggspec - a unit test framework and runner for GNU Guile.")
  (newline)
  (println "ggspec will look in the current directory's 'spec' subdirectory and run")
  (println "all tests it finds there.")
  (newline)
  (println "Usage: ggspec [-v|--version] [-h|--help] [-f|--format FMT] [-c|--colour]")
  (newline)
  (println "  -v")
  (println "  --version")
  (println "    Print version number of this command.")
  (newline)
  (println "  -h")
  (println "  --help")
  (println "    Print this help message.")
  (newline)
  (println "  -f FMT")
  (println "  --format FMT")
  (println "    Tell ggspec which output format to use. Formats are:")
  (println "      normal (default). ggspec's output format.")
  (println "      tap. output for the TAP test protocol.")
  (newline)
  (println "  -c")
  (println "  --colour")
  (println "    Tell ggspec to colour assertion success or failure messages.")
  (exit))

(define (show-version)
  (println "ggspec " version-num " " release-name)
  (println "Copyright (c) 2014 Yawar Amin.")
  (println "License: MIT <http://opensource.org/licenses/MIT>")
  (exit))

(define (main args)
  (define cmd-line-opts
    (getopt-long
      args
      '((version (single-char #\v) (value #f))
      (help (single-char #\h) (value #f))
      (colour (single-char #\c) (value #f))
      (format (single-char #\f) (value #t)))))
  (define output-cb (option-ref cmd-line-opts 'format "normal"))
  (define colour (option-ref cmd-line-opts 'colour #f))

  (if (option-ref cmd-line-opts 'help #f)
    (show-help))
  (if (option-ref cmd-line-opts 'version #f)
    (show-version))

  (let*
    ((opts
      (list
        `(option
          'output-cb
          ,(cond
            ((equal? output-cb "normal") output-normal)
            ((equal? output-cb "tap") output-tap)
            (#t output-none)))
        `(option 'colour ,colour)))
    (result (run-tree "spec" opts))
    (total (apply + result)))

    #!
    TODO: move the result printing into the output-cb functions, e.g.
    like:

    (output-cb
      #:tally-passed passed
      #:tally-failed failed
      #:tally-skipped skipped)
    !#
    (cond
      ((equal? output-cb "normal")
        (println
          "Total "
          total
          ", passed "
          (car result)
          ", failed "
          (cadr result)
          ", skipped "
          (caddr result)
          "."))
      ((equal? output-cb "tap")
        (println "1.." total)))))

;; vim:set ft=scheme:

